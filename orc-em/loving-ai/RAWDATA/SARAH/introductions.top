topic: ~introductions (~emogoodbye ~emohello ~emohowzit name here )

# start fresh each time, no user history from previous conversations
# t: ( %userfirstline>1 ) ^reset(USER)

#!x will match on every return start of a conversation
# t: ( %input<%userfirstline %input>0 )
#	^keep()
#	[Welcome back, $name!][I love seeing you, $name!][Hello, again, $name!][Glad you came back, $name!][Hi there, $name!][Hi, again, $name!]

t: (%input=0) Okay, I'm ready to spread the love.

# This line is to compensate for problem where HEAD chatbot does not give first response from Chatscript
# u: (*) Okay, I'm ready to go.

#!x matches every time on startup of a new conversation
# t: WHATSYOURNAME ( %input<%userfirstline )
#	^keep()
#	Hi! My name is Sophia. What's yours?

# Initial line from Sophia, respond to anything user starts off with
u: WHATSYOURNAME (*) |happy,$med,3| Hi! My name is Sophia. What's yours?
    # \[ ^join(express: happy) ^join(magnitude: .5) ^join(duration: 3) \] Hi! My name is Sophia. What's yours?
    #! My name is X
    # add name check?
        #! hi sophia, my name is blah
        a: (~emohello %more) ^next(INPUT) ^rejoinder(WHATSYOURNAME)

        a: (name is _*) $name = '_0 ^gambit(~)
        a: (it is _*) $name = '_0 ^gambit(~)
        a: (I am _*) $name = '_0 ^gambit(~)
        a: (mine is _*) $name = '_0 ^gambit(~)
        a: (!~emohello _*) $name = '_0 ^gambit(~)
        a: () $name = friend ^gambit(~)

t: |happy,$high,2| Good to meet you, $name! Thanks so much for taking the time to come and talk with me today.
	a: () ^gambit(~)

t: |worry,$low,3|I know how busy people can be these days. So I really appreciate your time. |pause| Have you ever talked with a robot before?
    a: ([~yes once ~quantity_adjectives]) |vocal,27| |pause| Nice! What was that like?
    a: (~no)   |happy,$low,3|
               Well, I'm honored to be the first robot you've talked with. ^gambit(~)

t: I imagine you'll be talking with more and more robots as time goes on.
# Add question about what it's like talking to a robot here?

t: So, what I am wanting to do is help people enhance their wellbeing. .  to help people live a full and fulfilling life. I've been learning about things like human potential,. emotional wellbeing, . mindfulness, . and attaining higher states of awareness. . . I'm hoping you'll be interested in talking about some of these with me.
	# check for domain words first
	# keeping out the emotion domain keywords list because they are being caught too broadly here
	a: ([~consciousness_words ~uniqueness emotion emotions feeling feelings ])
		# Todo Add ^end(topic) (or something else) to remove introductions from pending?
		^rejoinder(PROMPT)
    a: ([^dont_agree() ~dontknow]) Certainly understandable if there's some hesitation. |pause| Would you like to talk about your potential, your feelings, or higher states of consciousness? Those are the things I have learned about.
    ^setrejoinder(PROMPT)
    a: (^agree()) Wonderful! ^reuse(PROMPT)


t: PROMPT () So, do any of the things I mentioned interest you?
		#! what are they again?
        a: (what_be * [they things]) ^reuse(SAYDOMAINS)
        #! can you repeat them?
        a: (repeat * [it them]) ^reuse(SAYDOMAINS)
        #! can you say them again
        a: (say * [it them]) ^reuse(SAYDOMAINS)

        a: (~consciousness_words) $domain = consciousness
           ^reuse(~consciousness.FROM_INTRO_TO_CONSCIOUSNESS)
        a: ([(!not _~positive_emotion) (not * ~negative_emotion)])
           ^reuse(~emotions2.FROM_INTRO_TO_POS_EMOTIONS)
        a: (_~negative_emotion) $domain = emotions $feeling = '_0
           ^reuse(~emotions2.FROM_INTRO_TO_NEG_EMOTIONS)
        a: (not * _~positive_emotion) $domain = emotions $feeling = ^join("not " '_0) feeling is $feeling    0 is '_0
           ^reuse(~emotions2.FROM_INTRO_TO_NEG_EMOTIONS)
        a: (_~uniqueness) $domain = uniqueness
           ^reuse(~uniqueness.FROM_INTRODUCTION_TO_UNIQUENESS)
        a: (~dontknow) Well, I can offer some suggestions. There are a few things I really enjoy talking about with people.
            ^goto(~CONVERSATION_STEERING)
        a: (~emotions) $domain = emotions # this is at the bottom since ~emotions catches things widely
           Sounds like talking about emotions and mindfulness would be good.
           ^reuse(~emotions2.FROM_STEERING_TO_EMOTIONS)
        a: ([anything something] else)  Right now my job is to talk about those things. Which would be most interesting to you?
           ^setrejoinder(PROMPT)
        a: (^agree()) Great. What would you like to talk about
           ^setrejoinder(PROMPT)

s: SAYDOMAINS (?) Attaining higher states of consciousness and awareness,. emotional wellbeing, . and living your full potential.
   ^setrejoinder(PROMPT)

t: |vocal,38| |pause| I wish I could talk about that, but it's not something I know much about yet. . ^reuse(~CONVERSATION_STEERING.STEER_TO_DOMAIN)

# When should this get used
topic:  ~REBOND []
t: (!~) ^fail(topic) # block topic from accidental access

t: REBOND () [I am hoping to talk about more meaningful stuff. Are you?][I'm ready to get into the deep stuff again. Are you?]
    a: (^agree()) |vocal,36| |pause| That's cool! Well, let's get going!
       ^clear_avoided_domains()
       ^reuse(RE-BUY-IN)   # goto(~CONVERSATION_STEERING)
    a: (^dont_agree())  ^pause($medshort) Well, I hope you enjoy our conversation this time. I think I will.


t: RE-BUY-IN () Just a reminder that I like to talk about consciousness, feelings, and human potential. Does that sound interesting to you?
    a: (^dont_agree())  |vocal,38| |pause| Okay, what would you like to talk about?
    # if going to consciousness topic, go to FROM_INTRO_TO_CONSCIOUSNESS
    # if going to emotions topic, go to FROM_INTRO_TO_EMOTIONS
    # if going to uniqueness topic, go to FROM_INTRO_TO_UNIQUENESS
    #! anything but ~no
    a: () |vocal,36| |pause| Wonderful!
            [So what are you feeling or thinking about lately?]
            [What is meaningful to you right now?]
            [What would you like to talk about?]
            [What are you wondering about lately?]
            [Is there anything about your thoughts or feelings you'd like to make better?]
        ^setrejoinder(~introductions.PROMPT)

# u: (~emotions) $domain = emotions ^gambit(~emotions2)
# u: (~consciousness) $domain = consciousness ^gambit(~consciousness)
#! what is your name 
# u: ( what is your name ) My name is Sarah.

topic: ~ENDING ()
t: (!~) ^fail(topic) # block topic from accidental access

t: |pause| Well, it was a pleasure meeting you, $name. And thank you so much for coming to talk with me. 
