# ##############################################################################
# toy-introduction.ghost
#
# This is a reimplementation of the loving-ai introduction written in ghost.
# It follows the toy-goaldirected.ghost example and the introductions.top topic
# of the loving-ai Sarah implementation.
#
# ##############################################################################

# goal: (novelty=0.24) r: (eat apple) I want an apple

# emotion magnitudes
$min = .1
$low = .3
$med = .4
$high = .8
$max = 1

# $appreciate = 0

urge:(introduction=1.0)
ordered-goal: (introduction=1.0) # All following rules satisfy this goal 100%

  # Initial line from Sopha, respond to anything user starts off with
  r: WHATSYOURNAME (*) |happy,$med,3| Hi! My name is Sophia. What's yours?
  # Note: \[ ^join(express: happy) ^join(magnitude: .5) ^join(duration: 3) \]
    # j1: (~emohello %more) ^next(INPUT) ^rejoinder(WHATSYOURNAME)
    j1: (name is _*)     $name = '_0 ^reuse(GOOD_TO_MEET)
    j1: (it is _*)       $name = '_0 ^reuse(GOOD_TO_MEET)
    j1: (I am _*)        $name = '_0 ^reuse(GOOD_TO_MEET)
    j1: (mine is _*)     $name = '_0 ^reuse(GOOD_TO_MEET)
    j1: (!~emohello _*)  $name = '_0 ^reuse(GOOD_TO_MEET)
    j1: () $name = Maurizio

  # r: (*) |happy,$high,2| Good to meet you, $name!
  #         Thanks so much for taking the time to come and talk with me today.

  r: GOOD_TO_MEET ($name)
    |happy,$high,2| Good to meet you, $name!
                    Thanks so much for taking the time to come
                    and talk with me today.
    j1: (*) $appreciate = 1 ^reuse(APPRECIATE_TIME)

  r: APPRECIATE_TIME ($appreciate)
    |worry,$low,3|  I know how busy
                    people can be these days. So I really appreciate your
                    time. |pause| Have you ever talked with a robot before?
    j1: ([~yes once ~quantity_adjectives]) |vocal,27| |pause| Nice!
                                          What was that like?

    j1: (~no)   |happy,$low,3| Well, I'm honored to be the first robot
               you've talked with. # ^gambit(~)

  r: (^time_since_user_stopped_talking(0.5)) um.

  r: (^time_since_user_stopped_talking(3))
     I imagine you'll be talking with more and more robotcs as time goes on.

# Add question about what it's like talking to a robot here?

#! what is Loving AI?
r: (^time_since_user_stopped_talking(3))
   So, what I am wanting to do is help people enhance their wellbeing. .
   to help people live a full and fulfilling life. I've been learning about
   things like human potential,. emotional wellbeing, . mindfulness, . and
   attaining higher states of awareness. . . I'm hoping you'll be interested
   in talking about some of these with me.

   # check for domain words first
   # keeping out the emotion domain keywords list because they are being caught
   # too broadly here
   j1: ([~consciousness_words ~uniqueness_words emotion emotions feeling feelings
       ])
      ^rejoinder(PROMPT)
   j1: ([^dont_agree() ~dontknow]) Certainly understandable if there's some
                               hesitation. |pause| Would you like to talk
                               about your potential, your feelings, or
                               higher states of consciousness? Those are the
                               things I have learned about.
                               ^setrejoinder(PROMPT)
   j1: (^agree()) Wonderful! ^reuse(PROMPT)
