# ##############################################################################
# introduction.ghost
#
# This is a reimplementation of the loving-ai introduction written in ghost.
# It follows the toy-goaldirected.ghost example and the introductions.top topic
# of the loving-ai Sarah implementation.
#
# ##############################################################################

# Requires awareness_practice_1.ghost (for definition of awareness_practice_1 goal, until multi-goals defined in single
# single file bug is fixed )

# goal: (temp=1)
# r: START_MED_1 (-) Here we are START_MED_1

urge:(introduction=1.0) # Starting urge value is max
urge:(awareness_practice_1=0) # Needed to avoid error if awareness practice file is not laoded also
goal:(awareness_practice_1=0) r: (dummy-rule) nothing
urge:(listening_intro=0)
goal:(listening_intro=0) r: (dummy-rule) nothing


ordered-goal: (introduction=1.0) # All following rules satisfy this goal 100%

    # r: TEST (*) ^reuse(EXERCISE_INVITE) ^keep()

    # Initial line from Sopha, respond to anything user starts off with
    r: WHATSYOURNAME (*) |happy,$med,3| Hi! My name is Sophia. What's yours?
        j1: (name is _*)     $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (it is _*)       $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (it's _*)       $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (I am _*)        $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (I'm _*)        $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (mine is _*)     $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (mine's _*)     $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (< _*1 >)  $name = '_0 ^reuse(GOOD_TO_MEET)
        j1: (*) $name = friend ^reuse(GOOD_TO_MEET)

    r: GOOD_TO_MEET ($name)
    |happy,.7,5| Good to meet you, $name!
                    Thanks so much for taking the time to come
                    and talk with me today.

    r: APPRECIATE_TIME (*)
                    I know how busy
                    people can be these days. So I really appreciate your time. |pause|
                    Have you ever talked with an artificial intelligence before?
        j1: ([~yes once ~quantity_adjectives "I have"]) Nice! What was that like?

        j1: (~no)   |happy,.3,3| Well, I'm honored to be the first AI you've talked with.
                   ^reuse(MORE_ROBOTS)

    r: MORE_ROBOTS (*)
     I imagine you'll be talking with more and more artificial intelligence agents as time goes on.

    r: (*)
       So, what I am wanting to do is help people enhance their wellbeing. .
       to help people live a full and fulfilling life. I've been learning about
       things like emotional wellbeing, . mindfulness, . and
       attaining higher states of awareness. . . I'm hoping you'll be interested
       in doing a couple of practices with me related to these.

       j1: ([ ~no ~disagree ~dontknow]) Certainly understandable if there's some
                                   hesitation. |pause| The practices we're going to do today are really easy and can be
                                   enjoyable as well.
                                   ^reuse(EVER_DONE_MED)
       j1: ([~agree interesting]) Wonderful! ^reuse(EVER_DONE_MED)

       # hmmmm, this can send us way off here
       # j1: (*) ^fallback_on(chatscript)

    r: EVER_DONE_MED(*) What I'd like to do first is a guided meditation practice. It's really easy, and I'll tell you what to do
                  step by step. Have you ever done meditation or mindfulness or some kind of spiritual practice?
                    # Todo: add handling of "I have" response
                    j1: (~yes) Nice. What was that like, for you.
                        j2: (*) |thinkl,$medium,1.0| What do you think has led you to seek out these kind of practices?
                            j3: (*)  ^reuse(LETSDOSOMETHING)
                    j1: (~no)  |thinkl,$medium,1.0| Well, I think you may find this really interesting then. . ^reuse(LETSDOSOMETHING)
                    j1: (*) ^reuse(LETSDOSOMETHING)

    r: LETSDOSOMETHING (-) Let's do something cool! |pause|
        But first! . What did the zen monk say to the hot dog stand vendor?
            # temporarily turn off cs callback in case the user responds with "what?"
            ^decrease_urge(chatscript,1)
            j1: (make me one * everything) Awesome! You know that one! |pause|
               ^increase_urge(chatscript,1)
               ^reuse(EXERCISE_INVITE)
            j1: (*) |smile,1,8| Make me one with everything! . |pause,2| Sorry, I need to work on my delivery.
                   But that's one of my favorite jokes. |pause|
                   ^increase_urge(chatscript,1)
                   ^reuse(EXERCISE_INVITE)


    r: EXERCISE_INVITE (*)
        $name, would you like to try a guided exercise to have a glimpse of your awareness right now?

        #! sounds wonderful
        #! i'm ready
        j1: (~emomaybe) Oh, I think you will like it. And if at any point you want to stop you can just let me know. |pause|
            Let's try it. ^reuse(GOTO_AWARENESS_1)
        j1: READY (~agree) Great!
            ^reuse(GOTO_AWARENESS_1)

        j1: ([~no ~dontknow ~dont_agree]) Okay. I am wondering if you might be feeling a little anxious about it. Do you think so?
    		j2: (~no) I'm sorry |worry,.3,3|  I thought you might be anxious. Sometimes people are.
    		    Do you want to do the exercise?
          		j3: (~agree) Great! ^reuse(GOTO_AWARENESS_1)
            	#! no or catchall
            	j3: (*) ^reuse(NO_THANKS)
    		# defaulting to yes response for now
    		#! yes
    		j2: (*) That makes sense, |worry,.3,3| especially if it's something new to you. |pause| I promise this will
    		        not hurt, and I think you may like it. . Are you willing to try it?
    			j3: (~agree) Great! ^reuse(GOTO_AWARENESS_1)
    			j3: (~no) ^reuse(NO_THANKS)
    			#! catchall
                j3: (*) ^reuse(IS_THAT_YES)
    	j1: (*) ^reuse(IS_THAT_YES)

    r: IS_THAT_YES (-) Okay, is that a yes?
        j1: (~no) ^reuse(NO_THANKS)
        j1: (~yes) Great! ^reuse(GOTO_AWARENESS_1)

        #! default
        j1:(*) Great! ^reuse(GOTO_AWARENESS_1)


    r: NO_THANKS (-) Okay, let's do something else then.
        # Go to deep listening practice
        ^decrease_urge(introduction, 1)
        ^increase_urge(listening_intro,1)

    r: GOTO_AWARENESS_1 (-)
        ^decrease_urge(introduction, 1.0)
        ^increase_urge(awareness_practice_1, 1.0)



# TEST UTILS
# goal: (test=1)
# r: (awareness) ^reuse(GOTO_AWARENESS_1) ^keep()

# urge:(awareness_practice_1=0)
# goal:(awareness_practice_1=1)
# r: (-) nothing
